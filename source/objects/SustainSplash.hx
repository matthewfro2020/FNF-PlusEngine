package objects;

class SustainSplash extends FlxSprite
{
	public static var startCrochet:Float;
	public static var frameRate:Int;

	public var strumNote:StrumNote;
	public var linkedNoteSplash:NoteSplash; // Referencia al NoteSplash para heredar propiedades

	var timer:FlxTimer;

	public function new():Void
	{
		super();

		x = -50000;

		// Verificar si el archivo existe antes de cargarlo
		var atlasPath = 'holdCovers/holdCover-Vanilla';
		if (Paths.fileExists('images/$atlasPath.png', IMAGE) && Paths.fileExists('images/$atlasPath.xml', TEXT))
		{
			frames = Paths.getSparrowAtlas(atlasPath);
			animation.addByPrefix('hold', 'holdCover0', 24, true);
			animation.addByPrefix('end', 'holdCoverEnd0', 24, false);
			if(!animation.getNameList().contains("hold")) trace("Hold splash is missing 'hold' anim!");
		}
		else
		{
			// Usar un atlas por defecto o crear frames vacíos
			trace('Hold splash atlas not found: $atlasPath');
			makeGraphic(1, 1, 0x00000000); // Crear una imagen transparente
		}
	}

	override function update(elapsed)
	{
		super.update(elapsed);

		if (strumNote != null)
		{
			setPosition(strumNote.x, strumNote.y);
			visible = strumNote.visible;
			
			// Heredar propiedades dinámicas del NoteSplash si existe
			if (linkedNoteSplash != null && linkedNoteSplash.alive)
			{
				// Heredar cámara si cambió
				if (linkedNoteSplash.cameras != null && linkedNoteSplash.cameras.length > 0)
					cameras = linkedNoteSplash.cameras;
				
				// Heredar otras propiedades que puedan cambiar dinámicamente
				if (linkedNoteSplash.visible != visible && animation.curAnim?.name == "hold")
					visible = linkedNoteSplash.visible;
			}

			if (animation.curAnim?.name == "hold" && strumNote.animation.curAnim?.name == "static")
			{
				x = -50000;
				kill();
			}
		}
	}

	public function setupSusSplash(strum:StrumNote, daNote:Note, ?playbackRate:Float = 1, ?noteSplash:NoteSplash = null):Void
	{
		final lengthToGet:Int = !daNote.isSustainNote ? daNote.tail.length : daNote.parent.tail.length;
		final timeToGet:Float = !daNote.isSustainNote ? daNote.strumTime : daNote.parent.strumTime;
		final timeThingy:Float = (startCrochet * lengthToGet + (timeToGet - Conductor.songPosition + ClientPrefs.data.ratingOffset)) / playbackRate * .001;

		var tailEnd:Note = !daNote.isSustainNote ? daNote.tail[daNote.tail.length - 1] : daNote.parent.tail[daNote.parent.tail.length - 1];

		animation.play('hold', true, false, 0);
		if (animation.curAnim != null)
		{
			animation.curAnim.frameRate = frameRate;
			animation.curAnim.looped = true;
		}
		clipRect = new flixel.math.FlxRect(0, !PlayState.isPixelStage ? 0 : -210, frameWidth, frameHeight);

		if (daNote.shader != null)
		{
			shader = new objects.NoteSplash.PixelSplashShaderRef().shader;
			shader.data.r.value = daNote.shader.data.r.value;
			shader.data.g.value = daNote.shader.data.g.value;
			shader.data.b.value = daNote.shader.data.b.value;
			shader.data.mult.value = daNote.shader.data.mult.value;
		}

		strumNote = strum;
		linkedNoteSplash = noteSplash; // Vincular con el NoteSplash
		
		// Heredar valores de noteSplashData (alpha, antialiasing, etc)
		alpha = ClientPrefs.data.splashAlpha;
		if (daNote != null) alpha = daNote.noteSplashData.a;
		
		antialiasing = ClientPrefs.data.antialiasing;
		if (daNote != null) antialiasing = daNote.noteSplashData.antialiasing;
		if (PlayState.isPixelStage) antialiasing = false;
		
		// Heredar cámara del NoteSplash si existe
		if (linkedNoteSplash != null && linkedNoteSplash.cameras != null && linkedNoteSplash.cameras.length > 0)
			cameras = linkedNoteSplash.cameras;
		
		offset.set(PlayState.isPixelStage ? 112.5 : 106.25, 100);

		if (timer != null)
			timer.cancel();

		if (!daNote.hitByOpponent && alpha != 0)
			timer = new FlxTimer().start(timeThingy, (idk:FlxTimer) ->
			{
				if (!(daNote.isSustainNote ? daNote.parent.noteSplashData.disabled : daNote.noteSplashData.disabled) && animation != null)
				{
					alpha = 1;
					animation.play('end', true, false, 0);
					if (animation.curAnim != null)
					{
						animation.curAnim.looped = false;
						animation.curAnim.frameRate = 24;
					}
					clipRect = null;
					animation.finishCallback = (idkEither:Dynamic) ->
					{
						kill();
					}
					return;
				}
				kill();
			});
	}
}
